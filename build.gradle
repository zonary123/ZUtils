plugins {
  id 'java'
  id 'com.gradleup.shadow' version '9.3.1'
  id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

ext {
  hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
  patchline = findProperty("patchline") ?: "stable"
}

group = findProperty("group") ?: "dev.zonary123"
version = findProperty("version") ?: "1.0.0"
description = findProperty("description") ?: "A Zonary123 plugin."

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(25))
    vendor.set(JvmVendorSpec.ADOPTIUM)
  }
  withSourcesJar()
  withJavadocJar()
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  // --------------------
  // Lombok
  // --------------------
  compileOnly("org.projectlombok:lombok:1.18.42")
  annotationProcessor("org.projectlombok:lombok:1.18.42")

  testCompileOnly("org.projectlombok:lombok:1.18.42")
  testAnnotationProcessor("org.projectlombok:lombok:1.18.42")

  // --------------------
  // Hytale API
  // --------------------
  compileOnly(files("$hytaleHome/install/$patchline/package/game/latest/Server/HytaleServer.jar"))

  compileOnly(fileTree(dir: "libs", include: ["*.jar"]))

  // --------------------
  // SLF4J (PROVIDED BY HYTALE)
  // --------------------
  compileOnly("org.slf4j:slf4j-api:1.7.36")

  // --------------------
  // Runtime libraries (WITHOUT SLF4J)
  // --------------------
  implementation("com.github.ben-manes.caffeine:caffeine:3.2.3")

  implementation("com.zaxxer:HikariCP:2.3.2") {
    exclude group: "org.slf4j", module: "slf4j-api"
  }

  implementation("org.xerial:sqlite-jdbc:3.51.1.0") {
    exclude group: "org.slf4j", module: "slf4j-api"
  }

  implementation("org.mongodb:mongodb-driver-sync:5.6.2") {
    exclude group: "org.slf4j", module: "slf4j-api"
  }

  implementation("org.mongodb:bson:5.6.2") {
    exclude group: "org.slf4j", module: "slf4j-api"
  }

  implementation("net.objecthunter:exp4j:0.4.8")
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
  options.release.set(21)
}

tasks.javadoc {
  options.addStringOption('Xdoclint:-missing', '-quiet')
}

tasks.register('updatePluginManifest') {
  def manifestFile = file('src/main/resources/manifest.json')
  doLast {
    if (!manifestFile.exists()) {
      throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
    }
    def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
    manifestJson.Version = project.version
    manifestFile.text = groovy.json.JsonOutput.prettyPrint(
      groovy.json.JsonOutput.toJson(manifestJson)
    )
  }
}

tasks.named('processResources') {
  dependsOn 'updatePluginManifest'
  filteringCharset = 'UTF-8'
}

tasks.shadowJar {
  archiveBaseName.set(rootProject.name)
  archiveClassifier.set("")

  configurations = [project.configurations.runtimeClasspath]

  // --------------------
  // Relocations (SAFE)
  // --------------------
  relocate("com.mongodb", "dev.zonary123.libs.mongodb")
  relocate("org.bson", "dev.zonary123.libs.bson")

  // --------------------
  // CRITICAL: NEVER SHADE SLF4J
  // --------------------
  exclude "org/slf4j/**"
  exclude "META-INF/maven/**"
}

tasks.test {
  useJUnitPlatform()
}

tasks.build {
  dependsOn tasks.shadowJar
}

def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) serverRunDir.mkdirs()

idea.project.settings.runConfigurations {
  'HytaleServer'(org.jetbrains.gradle.ext.Application) {
    mainClass = 'com.hypixel.hytale.Main'
    moduleName = project.idea.module.name + '.main'
    programParameters =
      "--allow-op --assets=$hytaleHome/install/$patchline/package/game/latest/Assets.zip"

    if (findProperty("includes_pack")?.toBoolean()) {
      programParameters +=
        " --mods=${sourceSets.main.java.srcDirs.first().parentFile.absolutePath}"
    }

    if (findProperty("load_user_mods")?.toBoolean()) {
      programParameters += " --mods=$hytaleHome/UserData/Mods"
    }

    workingDirectory = serverRunDir.absolutePath
  }
}
